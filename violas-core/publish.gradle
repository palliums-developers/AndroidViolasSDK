apply plugin: 'maven-publish'

def getGitHubUrl = { ->
    return "https://maven.pkg.github.com/palliums-developers/AndroidCommonLib"
}

def LIB_GROUP_ID = 'org.palliums.lib'
def LIB_ARTIFACT_ID = 'violas-core'
def LIB_VERSION = '0.0.2'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        Properties properties = new Properties()
        File configFile = project.rootProject.file('local.properties')
        properties.load(configFile.newDataInputStream())

        maven {
            name = "GithubPackages"
            url = uri(getGitHubUrl())
            credentials {
                username = System.getenv('GITHUB_USER') ?: properties.getProperty('GITHUB_USER')
                password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: properties.getProperty('GITHUB_PERSONAL_ACCESS_TOKEN')
            }
        }
    }
    publications {
        lib(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/${LIB_ARTIFACT_ID}-release.aar")
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}